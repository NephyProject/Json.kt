group 'jp.nephy'
version '4.8'

buildscript {
    ext.kotlin_version = '1.3.21'

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://dl.bintray.com/kotlin/kotlin-eap" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
    }
}

apply plugin: "kotlin"
apply plugin: "kotlinx-serialization"
apply plugin: "maven"
apply plugin: "signing"

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url "https://kotlin.bintray.com/kotlin-eap" }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.10.0"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions.freeCompilerArgs += ["-Xuse-experimental=kotlin.contracts.ExperimentalContracts"]
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task javadocJar(type: Jar) {
    classifier = "javadoc"
    from javadoc
}
task sourcesJar(type: Jar) {
    classifier = "sources"
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { hasProperty("deploy") }
    sign configurations.archives
}

sourceSets {
    main.java.srcDirs += "src/main/kotlin"
}

if (hasProperty("deploy")) {
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                def env = System.getenv()

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                    authentication userName: env["sonatypeUsername"], password: env["sonatypePassword"]
                }
                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                    authentication userName: env["sonatypeUsername"], password: env["sonatypePassword"]
                }

                pom.project {
                    name "Json.Kt"
                    packaging "jar"
                    artifactId "jsonkt"
                    description "Json bindings for Kotlin."
                    url "https://github.com/NephyProject/Json.kt"

                    licenses {
                        license {
                            name "MIT"
                            url "http://opensource.org/licenses/MIT"
                        }
                    }

                    developers {
                        developer {
                            id "SlashNephy"
                            name "Slash Nephy"
                            email "admin@nephy.jp"
                        }
                    }

                    scm {
                        url "https://github.com/NephyProject/Json.Kt"
                        connection "scm:git:https://github.com/NephyProject/Json.Kt.git"
                        developerConnection "scm:git:git@github.com:NephyProject/Json.Kt.git"
                        tag "HEAD"
                    }

                    issueManagement {
                        system "GitHub Issues"
                        url "https://github.com/NephyProject/Json.kt/issues"
                    }
                }
            }
        }
    }
}
